plugins {
    id 'java'
    id 'org.springframework.boot' version "$orgSpringframeworkBoot"
    id 'io.spring.dependency-management' version "$ioSpringDependencyManagement"
    id 'org.openapi.generator' version "$orgOpenapiGenerator"
}

group = 'ru.ersted'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("$javaVersion")
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation "org.postgresql:postgresql:$orgPostgresqlPostgresql"

    implementation "org.mapstruct:mapstruct:$orgMapstructMapstruct"
    annotationProcessor "org.mapstruct:mapstruct-processor:$orgMapstructMapstructProcessor"

    testImplementation "org.mockito:mockito-core:$orgMockitoMockitoCore"
    testImplementation "org.mockito:mockito-junit-jupiter:$orgMockitoMockitoJunitJupiter"

    testImplementation "org.testcontainers:testcontainers:$orgTestcontainersTestcontainers"
    testImplementation "org.testcontainers:junit-jupiter:$orgTestcontainersJunitJupiter"
    testImplementation "org.testcontainers:postgresql:$orgTestcontainersPostgresql"

    compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'

}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    mainClass = 'ru.ersted.module_1spirngmvc.Module1SpirngMvcApplication'
    archiveBaseName.set('myapp-mvc')
    archiveVersion.set('')
    archiveClassifier.set('')
}

openApiGenerate {
    generatorName = 'java'
    inputSpec = file('src/main/resources/api/openapi.yaml')
            .toURI()
            .toString()
    outputDir = layout.buildDirectory
            .dir('generated')
            .get().asFile.absolutePath

    modelPackage = 'ru.ersted.module_1spirngmvc.dto.generated'
    apiPackage = 'com.example.stub.api'

    globalProperties = [models: '']

    configOptions = [
            useJakartaEe              : 'true',
            constructorWithAllArgs     : 'true',
            dateLibrary               : 'java8',
            library                   : 'resttemplate',
            generateModelTests        : 'false',
            generateModelDocumentation: 'false',
            generateApiTests          : 'false',
            generateApiDocumentation  : 'false'
    ]
}

compileJava {
    dependsOn tasks.openApiGenerate
    source layout.buildDirectory.dir('generated/src/main/java')
}